/**
 * Copyright 2013-2018 the original author or authors from the JHipster project.
 *
 * This file is part of the JHipster project, see http://www.jhipster.tech/
 * for more information.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
 
/**
 * @author Jose Badeau - Initial contribution
 * @author Serano Colameo - Initial contribution and enhancements
 */
 
grammar io.github.jhipster.jdl.JDL hidden(WS, ML_COMMENT, SL_COMMENT)

generate jdl "http://www.github.io/jhipster/jdl/JDL"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

/* =========================================================================
 * ROOT
 * ========================================================================= */

JdlDomainModel:
	features+=JdlFeature*;

JdlFeature:
	  JdlApplication 
	| JdlDeployment 
	| JdlConstant 
	| JdlEntity 
	| JdlEnum 
	| JdlRelationships 
	| JdlOption;

/* =========================================================================
 * APPLICTION
 * ========================================================================= */ 

JdlApplication: 
	'application' '{'
		config=JdlApplicationConfig
		selection=JdlApplicationEntitiesSelection?
		exclusion=JdlExceptEntityExclusion?
	'}';

JdlApplicationConfig:
	'config' {JdlApplicationConfig} '{'
		(paramters+=JdlApplicationParameter ','?)*
	'}';	

JdlApplicationEntitiesSelection:
	'entities' (predicate=JdlWildcardPredicate | selection=JdlEntitySelection);
			
JdlApplicationParameter:
	paramName=JdlApplicationParameterName paramValue=JdlParameterValue;

enum JdlApplicationParameterName:
	applicationType | authenticationType | baseName |
	buildTool | cacheProvider | clientFramework | clientPackageManager |
    databaseType | devDatabaseType | enableHibernateCache |
    enableSwaggerCodegen | enableTranslation | jhiPrefix |
	jhipsterVersion | languages | messageBroker |
	nativeLanguage | packageName | prodDatabaseType |
	searchEngine | serverPort | serviceDiscoveryType |
	skipClient | skipServer | skipUserManagement |
    testFrameworks | uaaBaseName | useSass | websocket;

/* =========================================================================
 * DEPLOYMENT
 * ========================================================================= */ 

JdlDeployment: 
	{JdlDeployment} 'deployment' '{'
		(paramters+=JdlDeploymentParameter ','?)*
	'}';

JdlDeploymentParameter:
	paramName=JdlDeploymentParameterName paramValue=JdlParameterValue;

enum JdlDeploymentParameterName:
	deploymentType | directoryPath |  appsFolders |
	clusteredDbApps | gatewayType | monitoring | consoleOptions |
    serviceDiscoveryType | dockerRepositoryName | dockerPushCommand |
    kubernetesNamespace | kubernetesServiceType | ingressDomain |
	istio | istioRoute | enableRancherLoadBalancing |
	openshiftNamespace | storageType;

/* =========================================================================
 * CONSTANT
 * ========================================================================= */

JdlConstant:
	name=ID '=' value=INT;

JdlNumericValue:
	constant=[JdlConstant] | value=INT;

/* =========================================================================
 * ENTITY
 * ========================================================================= */

JdlEntity:
	annotations+=JdlAnnotation*
	'entity' name=ID ('(' table=ID ')')? ('{'
		(fields+=JdlEntityField (','? fields+=JdlEntityField)*)?
	'}')?;

JdlEntityField:
	name=ID type=JdlFieldType;

JdlFieldType:
	  JdlEnumFieldType 
	| JdlStringFieldType 
	| JdlNumericFieldType 
	| JdlBooleanFieldType 
	| JdlDateFieldType 
	| JdlBlobFieldType 
	| JdlEntityType;

JdlEntityType:
	element=JdlEntity;

JdlEnumFieldType:
	element=[JdlEnum] validators+=JdlDefaultValidator*;

JdlStringFieldType:
	element=JdlStringType validators+=JdlStringValidator*;

JdlStringType:
	element='String';

JdlNumericFieldType:
	element=JdlNumericTypes validators+=JdlNumericValidator*;

enum JdlNumericTypes:
	Integer | Long | BigDecimal | Float | Double;

JdlBooleanFieldType:
	element=JdlBooleanType validators+=JdlDefaultValidator*;

JdlBooleanType:
	element='Boolean';

JdlDateFieldType:
	element=JdlDateTypes validators+=JdlDefaultValidator*;

enum JdlDateTypes:
	Date | LocalDate | ZonedDateTime | Instant | UUID;
	
JdlBlobFieldType:
	element=JdlBlobTypes validators+=JdlBlobValidator*;

enum JdlBlobTypes:
	Blob | AnyBlob | ImageBlob | TextBlob;

/* =========================================================================
 * VALIDATOR
 * ========================================================================= */

JdlStringValidator:
	required?=JdlRequiredValidator |
	unique?=JdlUniqueValidator |
	minlength=JdlMinLengthValidator |
	maxlength=JdlMaxLengthValidator |
	pattern=JdlPatternValidator;

JdlNumericValidator:
	required?=JdlRequiredValidator | 
	unique?=JdlUniqueValidator |
	min=JdlMinValidator | 
	max=JdlMaxValidator;

JdlBlobValidator:
	required?=JdlRequiredValidator |
	unique?=JdlUniqueValidator |
	minbytes=JdlMinBytesValidator | 
	maxbytes=JdlMaxBytesValidator;

 JdlDefaultValidator:
	required?=JdlRequiredValidator |
	unique?=JdlUniqueValidator;

JdlUniqueValidator:
	unique?='unique';

JdlRequiredValidator:
	required?='required';

JdlMinLengthValidator:
	'minlength' '(' value=JdlNumericValue ')';

JdlMaxLengthValidator:
	'maxlength' '(' value=JdlNumericValue ')';

JdlPatternValidator:
	'pattern' '(' value=REG_EXP ')';

JdlMinValidator:
	'min' '(' value=JdlNumericValue ')';

JdlMaxValidator:
	'max' '(' value=JdlNumericValue ')';

JdlMinBytesValidator:
	'minbytes' '(' value=JdlNumericValue ')';

JdlMaxBytesValidator:
	'maxbytes' '(' value=JdlNumericValue ')';

/* =========================================================================
 * RELATIONSHIP
 * ========================================================================= */

JdlRelationships:
	'relationship' cardinality=JdlCardinality '{'
		relationships+=JdlRelationship (','? relationships+=JdlRelationship)*
	'}';

JdlRelationship:
	source=JdlRelation 'to' target=JdlRelation;

JdlRelation:
	entity=[JdlEntity] role=JdlRelationRole?;

JdlRelationRole:
	'{' name=ID ((required?=JdlRequiredValidator)? & ('(' role=ID ')')?) '}';

enum JdlCardinality:
	OneToMany | ManyToOne | OneToOne | ManyToMany;

/* =========================================================================
 * ENUM
 * ========================================================================= */

JdlEnum:
	'enum' name=ID '{'
		values+=ID (','? values+=ID)*
	'}';

/* =========================================================================
 * ANNOTATION 
 * ========================================================================= */

JdlAnnotation:
	JdlComplexAnnotation |
	JdlSimpleAnnotation;

JdlComplexAnnotation:
	'@' (JdlDtoAnnotation |
	 JdlPaginateAnnotation |
	 JdlServiceAnnotation |
	 JdlMicroserviceAnnotation |
	 JdlSearchAnnotation |
	 JdlAngularSuffixAnnotation |
	 JdlClientRootFolderAnnotation);

JdlSimpleAnnotation:
	'@' (JdlSkipClientAnnotation |
		JdlSkipServerAnnotation |
		JdlFilterAnnotation |
		JdlNoFluentMethodAnnotation);

JdlDtoAnnotation returns JdlComplexAnnotation:
	dto?='dto' '(' dtoType=JdlDtoType ')';

JdlPaginateAnnotation returns JdlComplexAnnotation:
	paginate?='paginate' '(' paginateType=JdlPaginateType ')';
	
JdlServiceAnnotation returns JdlComplexAnnotation:
	service?='service' '(' serviceType=JdlServiceType ')';

JdlMicroserviceAnnotation returns JdlComplexAnnotation:
	microservice?='microservice' '(' name=ID ')';

JdlSearchAnnotation returns JdlComplexAnnotation:
	search?='search' '(' searchType=JdlSearchType ')';

JdlAngularSuffixAnnotation returns JdlComplexAnnotation:
	angularSuffix?='angularSuffix' '(' name=ID ')';

JdlClientRootFolderAnnotation returns JdlComplexAnnotation:
	clientRootFolder?='clientRootFolder';

JdlSkipClientAnnotation returns JdlSimpleAnnotation:
	skipClient?='skipClient';

JdlSkipServerAnnotation returns JdlSimpleAnnotation:
	skipServer?='skipServer';

JdlFilterAnnotation returns JdlSimpleAnnotation:
	skipServer?='filter';

JdlNoFluentMethodAnnotation returns JdlSimpleAnnotation:
	noFluentMethod?='noFluentMethod';

/* =========================================================================
 * OPTION 
 * ========================================================================= */

JdlOption:
	setting=JdlOptionSetting (excludes=JdlExceptEntityExclusion)?;

JdlOptionSetting:
	JdlDtoOption |
	JdlPaginateOption |
	JdlServiceOption |
	JdlMicroserviceOption |
	JdlSearchOption |
	JdlSkipClientOption |
	JdlSkipServerOption |
	JdlAngularSuffixOption |
	JdlClientRootFolderOption |
	JdlNoFluentMethodOption |
	JdlFilterOption;

JdlWithEntitySelectionAndValue returns JdlOptionSelection:
	(predicate=JdlWildcardPredicate | selection=JdlEntitySelection) 'with';

JdlForEntityInclusion returns JdlOptionSelection:
	(predicate=JdlWildcardPredicate | selection=JdlEntitySelection);

JdlExceptEntityExclusion:
	'except' selection=JdlEntitySelection;

JdlEntitySelection:
	entities+=[JdlEntity] (',' entities+=[JdlEntity])*;

JdlWildcardPredicate:
	wildcard?='*' | all?='all';

/* =========================================================================
 * DTO OPTION 
 * ========================================================================= */

JdlDtoOption returns JdlOptionSetting:
	dtoOption?='dto' includes=JdlWithEntitySelectionAndValue dtoType=JdlDtoType;

enum JdlDtoType:
	mapstruct;

/* =========================================================================
 * PAGINATION OPTION 
 * ========================================================================= */

JdlPaginateOption returns JdlOptionSetting:
	paginateOption?='paginate' includes=JdlWithEntitySelectionAndValue paginateType=JdlPaginateType;

JdlPaginateType:
	pagination?=('pager' | 'pagination') | infiniteScroll?='infinite-scroll';

/* =========================================================================
 * SERVICE OPTION 
 * ========================================================================= */

JdlServiceOption returns JdlOptionSetting:
	serviceOption?='service' includes=JdlWithEntitySelectionAndValue serviceType=JdlServiceType;

enum JdlServiceType:
	serviceClass | serviceImpl;

/* =========================================================================
 * MICRO SERVICE OPTION 
 * ========================================================================= */

JdlMicroserviceOption returns JdlOptionSetting:
	microserviceOption?='microservice' includes=JdlWithEntitySelectionAndValue appname=ID;

/* =========================================================================
 * SEARCH OPTION 
 * ========================================================================= */

JdlSearchOption returns JdlOptionSetting:
	searchOption?='search' includes=JdlWithEntitySelectionAndValue searchType=JdlSearchType;

enum JdlSearchType:
	elasticsearch;

/* =========================================================================
 * SKIP CLIENT OPTION 
 * ========================================================================= */

JdlSkipClientOption returns JdlOptionSetting:
	skipClientOption?='skipClient' includes=JdlForEntityInclusion;

/* =========================================================================
 * SKIP SERVER OPTION 
 * ========================================================================= */
JdlSkipServerOption returns JdlOptionSetting:
	skipServerOption?='skipServer' includes=JdlForEntityInclusion;

/* =========================================================================
 * ANGULAR SUFFIX OPTION 
 * ========================================================================= */
JdlAngularSuffixOption returns JdlOptionSetting:
	angularSuffixOption?='angularSuffix' includes=JdlWithEntitySelectionAndValue id=ID;

/* =========================================================================
 * CLIENT ROOT FOLDER OPTION 
 * ========================================================================= */

JdlClientRootFolderOption returns JdlOptionSetting:
	clientRootFolder?='clientRootFolder' includes=JdlWithEntitySelectionAndValue id=ID;

/* =========================================================================
 * Filter OPTION 
 * ========================================================================= */

JdlFilterOption returns JdlOptionSetting:
	filterOption?='filter' includes=JdlForEntityInclusion;

/* =========================================================================
 * NO FLUENT METHOD OPTION 
 * ========================================================================= */

JdlNoFluentMethodOption returns JdlOptionSetting:
	noFluentMethodOption?='noFluentMethod' includes=JdlForEntityInclusion;

/* =========================================================================
 * PARAMETER VALUE
 * ========================================================================= */ 

JdlParameterValue:
	{JdlParameterValue} 
	('[' listElements+=JdlParameterLiteral? (',' listElements+=JdlParameterLiteral)* ']')
	| (identifiers+=JdlParameterLiteral ('.' identifiers+=JdlParameterLiteral)*)
	| stringValue=STRING | numberValue=INT | version=JdlParameterVersion;

JdlParameterLiteral:
	ID | NUMERIC_PREFIXED_ID | JdlReserverdWords;

JdlParameterVersion:
	versionTag=VERSION_TAG; 

JdlReserverdWords:
	'microservice' | 'elasticsearch' | 'skipClient' | 'skipServer';

/* =========================================================================
 * TERMINAL DEFINITIONS
 * ========================================================================= */

terminal ID: ('a'..'z'|'A'..'Z'|'_'|'-') ('a'..'z'|'A'..'Z'|'_'|'-'|'0'..'9')*;
terminal NUMERIC_PREFIXED_ID returns ecore::EString: INT? ID;
terminal VERSION_TAG returns ecore::EString: '"' INT '.' INT ('.' INT)? ('-' ID)? '"';
terminal WS: (' '|'\t'|'\r'|'\n')+;
terminal INT returns ecore::EInt: ('0'..'9')+;
terminal STRING: '"' EXP '"' | "'" EXP "'";
terminal fragment EXP: ('\\' . | !('\\'|'"'|'\r'|'\n'))*;
terminal ML_COMMENT: '/*' -> '*/';
terminal SL_COMMENT: '//' ANY;
terminal REG_EXP: '/' ANY '/';
terminal fragment ANY: !('\n'|'\r'|'/')* ('\r'? '\n')?;

/* =========================================================================
 * END OF GRAMMAR DEFINITION 
 * ========================================================================= */
